ChrMap:
MAPPING LOAD * INLINE [

    From,           To

    á,                     a

    â,                     a

    ã,                    a

    é,                     e

    ê,                     e

    í,                     i

    ó,                     o

    ô,                     o

    õ,                    o

    ú,                     u

    Á,                     A

    Â,                     A

    Ã,                    A

    É,                     E

    Ê,                     E

    Í,                     I

    Ó,                     O

    Ô,                     O

    Õ,                    O

    Ú,                     U

    ç,                    c

    Ç,                    c

];


MapCaracteresEstranhos:
Mapping LOAD * inline [
'Caracter', 'Sobrescrever'
'.',''
'-',''
'/',''
' ',''
];

trace 'LENDO MAP IBGE';

//Municipios e UF - 
IBGE:
	LOAD State_ShortName, 
	     State_ID, 
	     Municipality_IBGE, 
	     Municipality_ID,
	     Municipality_FAZENDA
	FROM
	docs\IBGE.xls
	(biff, embedded labels, table is [Municipality$]);

MAP_IBGE:	
	MAPPING LOAD  
		State_ShortName & upper(mapsubstring('ChrMap', Municipality_FAZENDA)) as UF_MUNICIPIO,
		Municipality_ID AS COD_IBGE 
	RESIDENT IBGE;

DROP TABLE IBGE;

trace 'LENDO MAP CFOP';
CFOP:
	LOAD Cod, 
	     CFOP, 
	     DESCRIÇÃO, 
	     ATENÇÃO, 
	     DAPI, 
	     Reclassificação, 
	     DESCRIÇÃO1
	FROM
	docs\CFOP.xlsx
	(ooxml, embedded labels, table is De_para_CFOP);

MAP_CFOP:
	MAPPING load 
		CFOP,
		DESCRIÇÃO AS DESCRICAO_CFOP 
	RESIDENT CFOP;

MAP_CHECKCFOP:
	MAPPING load 
		CFOP,
		CFOP AS CHECK_CFOP 
	RESIDENT CFOP;
	
MAP_GRUPO_CFOP:
	MAPPING load 
		CFOP,
		Reclassificação AS GRUPO_CFOP 
	RESIDENT CFOP;
	
MAP_DAPI_CFOP:
	MAPPING load 
		CFOP,
		DAPI AS DAPI_CFOP 
	RESIDENT CFOP WHERE DAPI<>'';
	
MAP_GRUPO_NOME_CFOP:
	MAPPING load 
		Reclassificação,
		DESCRIÇÃO1 AS GRUPO_NOME_CFOP 
	RESIDENT CFOP;

//11 a 21 ou 44 a 54	Estado	
//22 a 32 ou 55 a 65	Outros Estados	
//33 a 43 ou 66 a 76	Exterior	

MAP_FLUXO_CFOP:
	MAPPING load 
		CFOP,
		IF( (Reclassificação>=11 AND Reclassificação<=21) OR (Reclassificação>=44 AND Reclassificação<=54),'Estado',
			IF( (Reclassificação>=22 AND Reclassificação<=32) OR (Reclassificação>=55 AND Reclassificação<=65),'Outros Estado',
				IF( (Reclassificação>=33 AND Reclassificação<=43) OR (Reclassificação>=66 AND Reclassificação<=76),'Exterior','')
			)
		)		AS FLUXO_CFOP 
	RESIDENT CFOP;
	
DROP TABLE CFOP;

trace 'LENDO MAP CNAE';
CNAE:
	LOAD @1 as Secao,	//a 
	     @2 as Divisao, 	//01
	     @3 as Grupo, 	//01.1
	     @4 as Classe, 	//01.11-3
	     MapSubString('MapCaracteresEstranhos',trim(@5)) as Subclasse,  //0111-3/03
	     @6 as Denominacao //Cultivo de Arroz
	FROM
	F:\Projetos\datavivaetl\nfe\docs\CNAE.xls
	(biff, no labels, table is [Est. Detalhada CNAE 2.1 - subcl$]);


	
MAP_CNAE:
	MAPPING load 
		Subclasse  AS CNAE,
		Denominacao AS DESCRICAO_CNAE 
	RESIDENT CNAE WHERE not isnull(Subclasse) and Subclasse<>'' and (len(Subclasse)=7  or  Subclasse='1' or Subclasse='2');

DROP TABLE CNAE;


NCM:
	LOAD NCM, 
	     DESCRIÇÃO
	FROM
	F:\Projetos\datavivaetl\nfe\docs\NCM.xlsx
	(ooxml, embedded labels, table is [NCM-TEC 2012]) WHERE len(NCM)=10 ;

MAP_NCM_DESCRICAO:
	MAPPING LOAD
		MapSubString('MapCaracteresEstranhos',trim(NCM)) as NCM,
		DESCRIÇÃO as DESCRICAO_NCM
	RESIDENT NCM;
	
MAP_NCM:
	MAPPING LOAD
		MapSubString('MapCaracteresEstranhos',trim(NCM)) as NCM,
		MapSubString('MapCaracteresEstranhos',trim(NCM)) as NCM_DESTINO
	RESIDENT NCM;

DROP TABLE NCM;

NCM_CORRECAO:
	LOAD NCM_fazenda, 
	     NCM_2012
	FROM
	F:\Projetos\datavivaetl\nfe\docs\NCMCorrecao.xlsx
	(ooxml, embedded labels, table is De_para_final);
	
MAP_NCM_CORRECAO:
	MAPPING LOAD
		NCM_fazenda as NCM_DE,
		NCM_2012 as NCM_PARA
	RESIDENT NCM_CORRECAO;	
	
DROP TABLE NCM_CORRECAO;